;; nyacc/foreign/cdata-02.test		-*- scheme -*-
;;
;; Copyright (C) 2024 Matthew Wette
;;
;; Copying and distribution of this file, with or without modification,
;; are permitted in any medium without royalty provided the copyright
;; notice and this notice are preserved.  This file is offered as-is,
;; without any warranty.

(define-module (cdata-01)
  #:use-module (nyacc foreign cdata)
  #:use-module (test-suite lib))

(define ctr
  (let ((p (make-count-reporter))) (register-reporter (car p)) ((cadr p))))
(register-reporter full-reporter)

(define (sferr fmt . args) (apply simple-format (current-error-port) fmt args))

(define t8 (name-ctype 't8 (cstruct `((o int) (p int)))))
(define t7 (name-ctype 't7 (cstruct `((m int) (n ,t8)))))
(define t6 (name-ctype 't6 (cstruct `((j int) (l ,(cpointer t7))))))
(define t5 (name-ctype 't5 (cstruct `((i int) (j ,t6)))))
(define t4 (name-ctype 't4 (cstruct `((g int) (h ,t5)))))
(define t3 (name-ctype 't3 (cstruct `((e int) (f ,(cpointer t4))))))
(define t2 (name-ctype 't2 (cstruct `((c int) (d ,t3)))))
(define t1 (name-ctype 't1 (cstruct `((a int) (b ,t2)))))

(define d7 (make-cdata t7))
(define d4 (make-cdata t4))
(define d1 (make-cdata t1))

(with-test-prefix "nyacc/foreign/cdata-02"

  (pass-if "foreign arch test set up"
    (let ()
      (cdata-set! d7 42 'n 'p)
      (cdata-set! d4 (cdata& d7) 'h 'j 'l)
      (cdata-set! d1 (cdata& d4) 'b 'd 'f)
      (cdata-set! d4 12 'g)
      (cdata-set! d7 23 'm)
      (cdata-set! d7 34 'n 'p) 
      #t))
      
  (pass-if "ctype-sel, getters, setters"
    (let* ((s1 (ctype-sel t1 0 'b 'd 'f '* 'g))
           (g1 (make-cdata-getter s1))
           (s2 (ctype-sel t1 0 'b 'd 'f '* 'h 'j 'l '* 'm))
           (g2 (make-cdata-getter s2))
           (s3 (ctype-sel t1 0 'b 'd 'f '* 'h 'j 'l '* 'n 'p))
           (g3 (make-cdata-getter s3)))
      (and (equal? (g1 d1) 12) (equal? (g2 d1) 23) (equal? (g3 d1) 34))))


  (pass-if "cdata make, ref, set!: multi-dimensioned arrays"
    (let* ((tri1-t (carray (carray (cbase 'double) 2) 3))
           (tval1 (make-cdata tri1-t #2f64((1.0 2.0) (3.0 4.0) (2.0 5.0)))))
      (for-each
       (lambda (ix)
         (for-each
          (lambda (jx)
            (cdata-set! tval1 (+ 0.5 (cdata-ref tval1 ix jx)) ix jx))
          (iota 2)))
       (iota 3))
      (array-equal? (cdata-ref tval1) #2f64((1.5 2.5) (3.5 4.5) (2.5 5.5)))))

  (pass-if "cdata make, ref, set!: array of structs"
    (let* ((loc-t (cstruct `((x double) (y double))))
           (tri2-t (carray loc-t 3))
           (tval2 (make-cdata tri2-t (vector '((x . 1.0) (y . 2.0))
                                             '((x . 3.0) (y . 4.0))
                                             '((x . 2.0) (y . 5.0))))))
      (for-each
       (lambda (ix)
         (for-each
          (lambda (m)
            (cdata-set! tval2 (+ 0.5 (cdata-ref tval2 ix m)) ix m))
          '(x y)))
       (iota 3))
      (equal? (cdata-ref tval2) #(((x . 1.5) (y . 2.5))
                                  ((x . 3.5) (y . 4.5))
                                  ((x . 2.5) (y . 5.5))))))

  )
      
(exit (if (positive? (assq-ref ctr 'fail)) 1 0))

;; --- last line ---
